plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'checkstyle'
    id 'jacoco'
    id 'application'
}

group 'uk.ac.ic.doc.blocc'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    implementation 'org.hyperledger.fabric-chaincode-java:fabric-chaincode-shim:2.5.0'
    implementation 'org.json:json:+'
    implementation 'com.owlike:genson:1.6'
    implementation 'org.jetbrains:annotations:23.0.0'
    testImplementation 'org.hyperledger.fabric-chaincode-java:fabric-chaincode-shim:2.5.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.mockito:mockito-core:5.+'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

application {
    mainClass = 'org.hyperledger.fabric.contract.ContractRouter'
}

checkstyle {
    toolVersion '10.11.0'
    configFile rootProject.file("config/checkstyle/google_checks.xml")
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
    finalizedBy jacocoTestReport
}

tasks.register('installLocalGitHook', Copy) {
    from new File(rootProject.rootDir, 'scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}

check.dependsOn jacocoTestCoverageVerification
installDist.dependsOn check
build.dependsOn installLocalGitHook